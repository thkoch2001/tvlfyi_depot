#!/usr/bin/env -S syndicate-server --control --config

let ?nix-actor = dataspace
$nix-actor
? ?any
[
  $log ! <log "nix-actor" { "+++": $any } >
  ?- [ $log ! <log "nix-actor" { "---": $any } > ]
]

<require-service <relay-listener <unix "/tmp/nix-actor.socket"> $gatekeeper>>
<bind <ref { oid: nix-actor key: #x"" }> $nix-actor #f>

@"Load test behavior."
<require-service <config-watcher "./tests" $.>>

@"Service instantiation."
<nix-provide-service nix-store>

? <nix-provide-service ?label>
[
  $log ! <log "nix-actor" { line: "service provisioning requested" label: $label }>
  ? <run-service <service $label ?detail>>
  [
    let ?result = <* $config [ <or [
        <rewrite <accepted ?body> <service-object <service $label $detail>    <ok $body>>>
        <rewrite <rejected ?body> <service-object <service $label $detail> <error $body>>>
      ]> ]>

    $log ! <log "nix-actor" { line: "require-service request" label: $label }>
    $nix-actor <resolve <$label $detail> $result>
  ]
]

? <run-service <service nix-repo ?detail>>
[
    let ?result = <* $config [ <or [
        <rewrite <accepted ?body> <service-object <service nix-repo $detail>    <ok $body>>>
        <rewrite <rejected ?body> <service-object <service nix-repo $detail> <error $body>>>
      ]> ]>
  $nix-actor <resolve <nix-repo $detail> $result>
]
